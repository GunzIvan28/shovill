#!/usr/bin/env perl
use strict;
use Data::Dumper;
use File::Slurp;
use File::Path qw(make_path remove_tree);
use File::Spec;
use File::Copy;
use List::Util qw(min max);
use Cwd;
use File::Basename;
use Time::Piece;
use Time::Seconds;
use FindBin;

# Globals
my $EXE = basename($0);
my $VERSION = "0.3";
my $URL = "https://github.com/tseemann/shovill";
my $AUTHOR = 'Torsten Seemann <torsten.seemann@gmail.com>';
my $OPSYS = $^O;
my $BAM = "$EXE.bam";
my $ADAPTERS = "$FindBin::RealBin/../db/trimmomatic.fa";
my %VALID_ASM = ('before_rr'=>1, 'contigs'=>1, 'scaffolds'=>1);
my $MIN_K = 31;   # sensible minimum, although prefer higher
my $MAX_K = 127;  # spades max
my $MINBQ = 3;    # for trimming and stats
my @LOG= ();      # global log file, saved at end

# Options
msg("You ran: $0 @ARGV");
my(@Options, $debug, $version, 
             $outdir, $force, $cpus, $tmpdir, $keepfiles,
             $kmers, $gsize, $R1, $R2, $opts,
             $nocorr, $trim, $trimopt, $asm, $minlen, $mincov);
setOptions();

# Say hello
msg("Hello", $ENV{USER} || 'stranger');
msg("This is $EXE $VERSION");
msg("Written by $AUTHOR");
msg("Homepage is $URL");
msg("Operating system is $OPSYS");
my $starttime = localtime;
msg("Local time is $starttime");

# Check options
-r $R1 or err("Can't read --R1 $R1");
-r $R2 or err("Can't read --R2 $R2");
-r $ADAPTERS or err("Can't see adapter file: $ADAPTERS");
sub is_integer { return $_[0] =~ m/^(\d+)$/ if defined $_[0]; }
$gsize = $gsize ? guess_bp($gsize) : 0;
is_integer($cpus) or err("Invalid --cpus $cpus");
is_integer($minlen) or err("Invalid --minlen $minlen");
is_integer($mincov) or err("Invalid --mincov $mincov");
my $lesscpus = min(1, max(4, int($cpus/4)));  # for samtools sort in pipe
$VALID_ASM{ $asm } or err( "Invalid --asm must be one of: ",join(' ', keys %VALID_ASM) );

# Check we have the required binaries
for my $exe (qw(seqtk kmc trimmomatic lighter flash spades.py bwa samtools java pilon tee)) {
  my $fullexe = find_exe($exe);
  $fullexe ? msg("Using $exe - $fullexe") : err("Could not find '$exe' - please install it.");
}

# Make output folder
make_folder($outdir);
$outdir = File::Spec->rel2abs($outdir);
$R1 = File::Spec->rel2abs($R1);
$R2 = File::Spec->rel2abs($R2);

msg("Changing into folder: $outdir");
my $cwd = getcwd();
chdir($outdir);

# Get some read stats
msg("Collecting raw read statistics with 'seqtk'");
my $stat = read_stats($R1);
map { msg("Read stats: $_ = ".$stat->{$_}) } (keys %$stat);

# Estimate genome size (or use genome size provided)
unless ($gsize) {
  msg("Estimating genome size with 'kmc'");
  # FIXME: want to choose a dynamic kmer cutoff for high cov samples
  my $minkc = 3;  # max(3, int($depth/100));
  run_cmd("kmc -ci$minkc -k25 -t$cpus \Q$R1\E kmc $tmpdir", "20-kmc.log");
  my($ucount) = grep { m/unique\s+counted/ } read_file('20-kmc.log');
  $ucount =~ m/(\d+)$/ or err("Could not determine unique counted k-mers using kmc");
  $gsize = $1;
}
msg("Using genome size $gsize bp");

# Estimate sequencing depth
my $depth = int( $stat->{'total_bp'} / $gsize );
msg("Estimated sequencing depth: $depth x");

# Get reads: if trimming, trim the originals into this folder, otherwise symlink
if ($trim) {
  msg("Trimming reads");
  run_cmd(
    "trimmomatic PE -threads $cpus -phred33".
    " \Q$R1\E \Q$R2\E R1.fq.gz /dev/null R2.fq.gz /dev/null $trimopt",
    "30-trimmomatic.log",
  );
}
else {
  run_cmd("ln -s \Q$R1\E R1.fq.gz");
  run_cmd("ln -s \Q$R2\E R2.fq.gz");
}

# Calculating read length distribution
my $RLEN = $stat->{'avg_len'};
msg("Average read length looks like $RLEN bp");
$MAX_K = min( $MAX_K, int(0.8 * $RLEN) );  # FIXME: make 0.8 based on fqchk QUAL?
msg("Setting maximum k-mer to $MAX_K");
unless ($minlen) {
  msg("Setting --minlen to $RLEN");
  $minlen = $RLEN
}

# Choosing some kmers
unless ($kmers) {
  $MIN_K = 21 if $stat->{'avg_len'} < 75;
  my $kn = 4; # max(4, $cpus);
  my $ks = max(5, int( ($MAX_K - $MIN_K) / ($kn-1) ) );
  $ks++ if $ks % 2 == 1; # need even step to ensure odd values only
  my @kmers;
  for (my $k=$MIN_K; $k < $MAX_K; $k+=$ks) {
    push @kmers, $k;
  }
  msg("Estimated K-mers: @kmers [kn=$kn, ks=$ks, kmin=$MIN_K, kmax=$MAX_K]");
  $kmers = join(',', @kmers);
}
msg("Using kmers: $kmers");

# Correct reads
msg("Correcting reads with 'Lighter'");
run_cmd(
  "lighter -od . -r R1.fq.gz -r R2.fq.gz -K 32 $gsize -t $cpus -maxcor 1",
  "40-lighter.log"
);

# Overlap corrected reads
msg("Overlapping reads with 'FLASH'");
run_cmd("flash -d . -o flash -z -M 300 -t $cpus R1.cor.fq.gz R2.cor.fq.gz", 
  "50-flash.log"
);

# Running Spades
msg("Assembling reads with SPAdes");
run_cmd(
  "spades.py -1 flash.notCombined_1.fastq.gz -2 flash.notCombined_2.fastq.gz -s flash.extendedFrags.fastq.gz"
 ." --only-assembler --threads $cpus --memory 32 -o . --tmp-dir $tmpdir -k $kmers $opts",
 "/dev/null",  # spades makes its own log file
);

# Correct contigs with Pilon
unless ($nocorr) {
  my $logfile = "70-bwa.log";
  my $target = "$asm.fasta";
  -r $target or err("Can not see '$target' file to correct!");
  msg("Checking for assembly errors in $target");
  run_cmd("bwa index $target", $logfile);
  run_cmd("(bwa mem -v 3 -x intractg -t $cpus $target R1.fq.gz R2.fq.gz"
         ." | samtools sort --threads $lesscpus -m 4G --reference $target -T $tmpdir/samtools.$$ -o $BAM)", 
         $logfile);
  run_cmd("samtools index $BAM", $logfile);

  msg("Correcting errors in $target");
  run_cmd(
    "pilon --genome $target --frags $BAM --output pilon --threads $cpus --changes --mindepth 0.5",
    "80-pilon.log");
  move($target, "$target.uncorrected");
  move("pilon.fasta", $target);
}
else {
  msg("User supplied --nocorr, so not correcting contigs.");
}

# Write final answer with nicer names
my $ncontigs = 0;
my $nbases = 0;
my $seq = read_fasta("$asm.fasta");
my %len = map { ( $_ => length($seq->{$_}) ) } (keys %{$seq});
for my $id (sort { $len{$b} <=> $len{$a} } keys %{$seq}) {
  # NODE_1_length_114969_cov_29.8803_pilon
  $id =~ m/cov_(\d+(\.\d+)?)/ or err("Could not get coverage of contig: $id");
  my $cov = $1;
  if ($len{$id} < $minlen) {
    msg("Removing short conting (< $minlen bp): $id");
    delete $seq->{$id};
  } 
  elsif ($cov < $mincov) {
    msg("Removing low coverage contig (< $mincov x): $id");
    delete $seq->{$id};
  }
  else {
    $ncontigs++;
    $nbases += $len{$id};
    my $newid = sprintf "contig%05d %s", $ncontigs, $id;
    $seq->{$newid} = $seq->{$id};
    delete $seq->{$id};
  }
}
write_fasta("contigs.fa", $seq);

msg("Writing $EXE log file.");
move("spades.log", "60-spades.log");

# Cleanup time!
unless ($keepfiles) {
  # corrected, overlapped and symlinked original reads
  unlink glob("*q.gz");
  # BWA indices
  unlink glob("$asm.fasta.*");
  unlink $BAM, "$BAM.bai";
  # Spades Kmer folders
  remove_tree( map { "K$_" } split m/,/, $kmers );
  # KMC
  unlink glob("kmc.*");
  # other gumpf
  remove_tree( 'misc' );
  unlink glob("*.paths");
  unlink "params.txt", "dataset.info", "flash.histogram", "input_dataset.yaml";
}

# Say our goodbyes
my $endtime = localtime;
my $walltime = $endtime - $starttime;
my $pretty = sprintf "%.2f minutes", $walltime->minutes;
msg("Walltime used: $pretty");
#msg("If you use this result please cite the Shovill paper:");
#msg("Seemann T (2017) Shovill: rapid prokaryotic genome assembly. BioRxiv doi:XXXX-YYYY");
#msg("Type '$EXE --citation' for more details.");

msg("Results in: $outdir");
msg("Final assembly in: $outdir/contigs.fa");
msg("It contains $ncontigs (min=$minlen) contigs totalling $nbases bp."); 

# Inspiration
my @motd = (
  "More correct contigs is better than fewer wrong contigs.",
  "A shovel will move more dirt than a spade.",
  "Wishing you a life free of misassemblies.",
  "Remember - genomic repeats > ~800bp will be collapsed into single contigs.",
  "Remember, an assembly is just a _hypothesis_ of the original sequence!",
  "Use Bandage to inspect the .gfa/.fastg assembly graph: https://rrwick.github.io/Bandage/",
  "Found a bug in $EXE? Post it at $URL/issues",
  "Have a suggestion for $EXE? Tell me at $URL/issues",
  "The $EXE manual is at $URL/blob/master/README.md",
  "Did you know? $EXE is a play on the words 'Spades' and 'Illumina' ('shovel')",
  "The name '$EXE' is pronounced as 'shovel' in English",
);
srand( $$ + $walltime + $MAX_K ); # seed
msg( $motd[ int(rand(scalar(@motd))) ] );
msg("Done.");

# Dump log and return to original folder
write_file("00-$EXE.log", @LOG);
chdir($cwd);
exit(0);

#----------------------------------------------------------------------
sub read_stats {
  my($R1) = @_;
  my $outfile = "10-seqtk.tab";
  my $stat;
  run_cmd("seqtk fqchk -q$MINBQ \Q$R1\E > $outfile");
  my @row = read_file($outfile);
#  msg($row[0]);
  for my $tag ('min_len', 'max_len', 'avg_len') {
    $row[0] =~ m/$tag:\s*(\d+(\.\d+)?);/ or err("Can't parse $tag from: $row[0]");
    $stat->{$tag} = int( $1 + 0.5 );
  }
  $row[2] =~ m/^ALL\s+(\d+)/ or err("Can't parse ALL #bases from: $row[2]");
  $stat->{'total_bp'} = $1 * 2;  # multiply by 2 as only using R1 (hack)
  return $stat;
}

#----------------------------------------------------------------------
sub make_folder {
  my($outdir) = @_;
  if (-d $outdir) {
    if ($force) {
      msg("Forced removal of existing --outdir $outdir (please wait)");
      remove_tree($outdir);
    }
    else {
      err("Folder '$outdir' already exists. Try using --force");
    }
  }
  make_path($outdir);
}

#----------------------------------------------------------------------
sub guess_bp {
  my($s) = @_;
  my %mult = ('G'=>1E9,'M'=>1E6,'K'=>1E3);
  $s =~ m/^([\d\.]+)([GMK])?$/i or die "Couldn't parse '$s'";
  my $bp = $1;
  $bp = $bp * $mult{uc($2)} if defined $2;
  return $bp;
}

#----------------------------------------------------------------------
sub run_cmd {
  my($cmd, $logfile) = @_;
  if ($logfile) {
    $cmd .= $debug ? " 2>&1 | tee -a $logfile" : " >> $logfile 2>&1";
  }
  msg("Running: $cmd");
  system($cmd)==0 or err("Error $? running command");
}

#----------------------------------------------------------------------
sub find_exe {
  my($bin) = shift;
  for my $dir (File::Spec->path) {
    my $exe = File::Spec->catfile($dir, $bin);
    return $exe if -x $exe;
  }
  return;
}

#----------------------------------------------------------------------
sub msg {
  my $msg = "@_\n";
  print STDERR $msg;
  push @LOG, $msg;
}
      
#----------------------------------------------------------------------
sub err {
  msg(@_);
  exit(1);
}

#----------------------------------------------------------------------
sub num_cpus {
  my($num)= qx(getconf _NPROCESSORS_ONLN); # POSIX
  chomp $num;
  return $num || 1;
}

#----------------------------------------------------------------------
sub version {
  print "$EXE $VERSION\n";
  exit(0);
}
   
#----------------------------------------------------------------------
sub read_fasta {
  my($fname) = @_;
  my $seq;
  my $id;
  open my $FASTA, '<', $fname or err("Could not open $fname");
  while (my $line = <$FASTA>) {
    chomp $line;
    if ($line =~ m/^>(\S+)/) {
      $id = $1;
      $seq->{$id} = '';
    }
    else {
      $seq->{$id} .= $line;
    }
  }
  close $FASTA;
  return $seq;
}

#----------------------------------------------------------------------
sub write_fasta {
  my($fname, $seq) = @_;
  open my $FASTA, '>', $fname or err("Could not write to $fname");
  for my $id (sort { $a cmp $b } keys %{$seq}) {
    print $FASTA ">$id\n", $seq->{$id}, "\n";
  }
  close $FASTA;
}
   
#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help",    VAR=>\&usage,             DESC=>"This help"},
    {OPT=>"debug!",  VAR=>\$debug, DEFAULT=>0, DESC=>"Debug info"},
    {OPT=>"version!",  VAR=>\&version, DESC=>"Print version and exit"},
    {OPT=>"cpus=i",  VAR=>\$cpus, DEFAULT=>min(16, num_cpus()), DESC=>"Number of CPUs to use"},
    {OPT=>"outdir=s",  VAR=>\$outdir, DEFAULT=>'', DESC=>"Output folder"},
    {OPT=>"force!",  VAR=>\$force, DEFAULT=>0, DESC=>"Force overwite of existing output folder"},
    {OPT=>"R1=s",  VAR=>\$R1, DEFAULT=>'', DESC=>"Read 1 FASTQ"},
    {OPT=>"R2=s",  VAR=>\$R2, DEFAULT=>'', DESC=>"Read 2 FASTQ"},
    {OPT=>"gsize=s",  VAR=>\$gsize, DEFAULT=>'', DESC=>"Estimated genome size <blank=AUTO>"},
    {OPT=>"kmers=s",  VAR=>\$kmers, DEFAULT=>'', DESC=>"K-mers to use <blank=AUTO>"},
    {OPT=>"opts=s",  VAR=>\$opts, DEFAULT=>'', DESC=>"Extra SPAdes options eg. --plasmid --careful ..."},
    {OPT=>"nocorr!",  VAR=>\$nocorr, DEFAULT=>0, DESC=>"Disable post-assembly correction"},
    {OPT=>"trim!",  VAR=>\$trim, DEFAULT=>0, DESC=>"Use Trimmomatic to remove common adaptors first"},
    {OPT=>"trimopt=s",  VAR=>\$trimopt, DEFAULT=>"ILLUMINACLIP:$ADAPTERS:1:30:11 LEADING:$MINBQ TRAILING:$MINBQ MINLEN:30 TOPHRED33", DESC=>"Trimmomatic options"},
    {OPT=>"minlen=i",  VAR=>\$minlen, DEFAULT=>0, DESC=>"Minimum contig length <0=AUTO>"},
    {OPT=>"mincov=f",  VAR=>\$mincov, DEFAULT=>2, DESC=>"Minimum contig coverage <0=AUTO>"},
    {OPT=>"asm=s",  VAR=>\$asm, DEFAULT=>'contigs', DESC=>"Spades result to correct: before_rr contigs scaffolds"},
    {OPT=>"tmpdir=s",  VAR=>\$tmpdir, DEFAULT=>$ENV{TMPDIR} || '/tmp', DESC=>"Fast temporary directory"},
    {OPT=>"keepfiles!",  VAR=>\$keepfiles, DEFAULT=>0, DESC=>"Keep intermediate files"},
  );

  (!@ARGV) && (usage());

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

#----------------------------------------------------------------------
sub usage {
  print "Synopsis:\n  Faster de novo assembly pipeline based around Spades\n";
  print "Usage:\n  $EXE [options] --outdir DIR --R1 R1.fq.gz --R2 R2.fq.gz\n";
  print "Author:\n  $AUTHOR\n";
  print "Options:\n";
  foreach (@Options) {
    printf "  --%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(1);
}
 
#----------------------------------------------------------------------
